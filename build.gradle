apply plugin: 'java'
apply plugin: 'signing'
apply plugin: 'maven'

group = 'com.saptarshidebnath.utilities'
archivesBaseName = 'ProcessRunner'
version = '0.0.3.0'

sourceCompatibility = 1.8
targetCompatibility = 1.8


tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'com.google.code.findbugs', name: 'annotations', version: '2.0.0'
    compile group: 'com.google.code.gson', name: 'gson', version: '2.8.0'
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.hamcrest', name: 'hamcrest-library', version: '1.3'
    testCompile group: 'org.apache.commons', name: 'commons-lang3', version: '3.5'
}

task sourceJar(type: Jar, dependsOn: classes) {
    from sourceSets.main.allJava
    classifier = 'sources'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar, sourceJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name(name)
                packaging 'jar'
                // optionally artifactId can be defined here
                description 'Process Runner is java based library using which one can execute system process ' +
                        'or scripts [shell scripts, batch file, python scripts, ruby scripts etc] from ' +
                        'inside your java program.'
                url 'http://code.saptarshidebnath.com/processrunner/'
                scm {
                    connection 'scm:git:git@github.com:saptarshidebnath/processrunner.git'
                    developerConnection 'scm:git:git@github.com:saptarshidebnath/processrunner.git'
                    url 'https://github.com/saptarshidebnath/processrunner'
                }

                licenses {
                    license {
                        name 'MIT License'
                        url 'https://github.com/saptarshidebnath/processrunner/blob/master/LICENSE.txt'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'saptarshi.devnath'
                        name 'Saptarshi Debnath'
                        email 'saptarshi.devnath@gmail.com'
                    }
                }
            }
        }
    }
}

//
// Built from the tutorial http://jedicoder.blogspot.com/2011/11/automated-gradle-project-deployment-to.html
//